// File handles UI and control creation for ThunderLoomMtlNode.


// Make sure this order in TL_params matches with ThunderLoomWriteCommand
// in maya_thunderloom.cpp.
global string $TL_params[] = {
    "bend",
    "yarnsize",
    "twist",
    "alpha",
    "beta",
    "specularColor",
    "specularColorAmount",
    "specularNoise",
    "highlightWidth",
    "diffuseColor",
    "diffuseColorAmount"
};


// Calls the exported c++ command to load given file and return the ThunderLoom
// patterns. Sets the maya attributes according to these values.
// Also disconnects any maps.
global proc setTLAttributesFromFile(string $nodeDotAttr) {
    string $buffer[];
	tokenize($nodeDotAttr, ".", $buffer);
	string $nodeName = $buffer[0];
    
	string $filepath = `getAttr ($nodeName + ".filepath")`;
    float $tlparams[] = `thunderLoomParseFile -fileName $filepath`;
    int $num_yarn_types = $tlparams[0];
    

    // Remove existing multies!
    // Use bend attribute to see how many multi-attributes we have of it,
    // (this will tell us the number of yarn_types currently on UI).
    string $multi[] = `listAttr -multi -leaf ($nodeName + ".bend")`;
    
    // Skip resetting default yarn if checkbox is on.
    // And only if there already are controls for default yarn [0].
    int $skipyrn0 = 0;
    if (`checkBox -q -v tlSkipDefaultYrnChk` && `stringArrayContains "bend[0]" $multi`) {
        $skipyrn0 = 1;
    }
    
    global string $TL_params[];

    for($m in $multi) {
		string $index = match("\[[0-9]+\]", $m); // Index with []
        if ($skipyrn0 != 0 && $index != "[0]") {
            for ($param in $TL_params) {
                removeMultiInstance -break true ($nodeName + "." + $param + $index);
                removeMultiInstance -break true ($nodeName + "." + $param + "On" + $index);
            }
        }
    }
    
    // Set Parameters
    // The order of tlparams is determined by ThunderLoomCommand in 
    // maya_thunderloom.cpp
    int $params_per_yarn = 15; //Should match number of params being added in for loop.
    int $param_index = 1; // Start from 1 to skip num_yarn_types.
    int $yrn_index = 0;
    if ($skipyrn0 != 0) {
        $yrn_index = 1;
    }
    while ($yrn_index<$num_yarn_types) { //For each yarn type
        string $yrn_index_str = ("[" + $yrn_index + "]");
        for ($param in $TL_params) { // For each parameter
            // Skip beta parameter
            if ($param == "beta") {
                $param_index = $param_index + 2; //Skip param and it's On value.
                continue;
            }

            // Special code for color parameters
            if ($param == "diffuseColor" || $param == "specularColor") {
                float $r = $tlparams[$param_index];
                float $g = $tlparams[$param_index + 1];
                float $b = $tlparams[$param_index + 2];
                setAttr ($nodeName + "." + $param + $yrn_index_str) $r $g $b;
                $param_index = $param_index + 3;
            } else {
                // Float parameters
                setAttr ($nodeName + "." + $param + $yrn_index_str) $tlparams[$param_index];
                $param_index = $param_index + 1;
            }
            
            // enabled parameters
            setAttr ($nodeName + "." + $param + "On" + $yrn_index_str) $tlparams[$param_index];
            $param_index = $param_index + 1;
        }
        $yrn_index = $yrn_index + 1;
    }
}

// Calls the exported c++ command to save parameters to file.
global proc saveTLAttributesToFile(string $nodeDotAttr) {
    string $buffer[];
	tokenize($nodeDotAttr, ".", $buffer);
	string $nodeName = $buffer[0];
	
    string $filepath = `getAttr ($nodeName + ".filepath")`;


    string $res = `confirmDialog -button "Cancel" -cancelButton "Cancel" -button "Save" -defaultButton "Save" -message ("ThunderLoom: Are you sure you wan't to write current yarn parameters to file: " + $filepath + "?")`;

    if ($res != "Save") {
        return;
    }

    // Make sure this order in TL_params matches with ThunderLoomWriteCommand
    // in maya_thunderloom.cpp.
	float $param_values[];
    global string $TL_params[];

    int $i = 0;
    // For each yarn type.
    string $multi[] = `listAttr -multi -leaf $nodeDotAttr`;
    for($m in $multi) {
        // Yarn type index as string with brackets [].
        string $yrn_index;
        $yrn_index = match("\[[0-9]+\]", $m); 

        for ($param in $TL_params) {
            if ($param == "specularColor" || $param == "diffuseColor") {
                float $values[] = `getAttr($nodeName + "." + $param + $yrn_index)`;
                $param_values[$i] = $values[0]; $i = $i + 1;
                $param_values[$i] = $values[1]; $i = $i + 1;
                $param_values[$i] = $values[2]; $i = $i + 1;
            } else {
                $param_values[$i] = `getAttr($nodeName + "." + $param + $yrn_index)`;
                $i = $i + 1;
            }
            $param_values[$i] = `getAttr($nodeName + "." + $param + "On" + $yrn_index)`;
            $i = $i + 1;
        }
    }

    //print("ThunderLoom params_values:");
    //print($param_values);

    if ( catch( `thunderLoomSaveFile $filepath $param_values` ) ) {
        print("ThunderLoomSaveFile error! Unable to save file.");
        confirmDialog -message "ThunderLoom: Error, unable to save parameters to file.";
    } else {
        confirmDialog -message "ThunderLoom: Saved parameters to pattern file.";
    }
    
}

// UI setup for file loading and global ThunderLoom settings.
// This function gets called by Maya when VRayThunderLoomMtl Node is opened in
// editor.
global proc AEVRayThunderLoomMtlTemplate(string $nodeName )
{
    editorTemplate -beginScrollLayout;
    
    editorTemplate -beginLayout "Pattern" -collapse false;
    // We want to call AEBrowseFileButton, AEBrowseFileButtonRepl when opening
    // file. Second string argument is used to specify a command to be executed
    // when the attribute is changed.
    editorTemplate -callCustom "AEBrowseFileButton" "AEBrowseFileButtonRepl" "browseFile";
    editorTemplate -label "Filepath" -addControl "filepath"; 
    editorTemplate -callCustom "AESkipDefaultYrn" "AESkipDefaultYrnRepl" "skipyrn0";
    editorTemplate -label "uscale" -addControl "uscale";
    editorTemplate -label "vscale" -addControl "vscale";
    editorTemplate -label "uvrotation" -addControl "uvrotation";
    editorTemplate -endLayout;

    // '-callCustom specifies function to use to build UI, second argument is 
    // a replacing function to use if attribute editor already exists and node
    // of same type should be edited.'
    editorTemplate -callCustom "AELoadYarnTypesCompound" "AELoadYarnTypesCompoundRepl" "yarnsize";

    // Specify place for new controls to be added.
    editorTemplate -addExtraControls;

    editorTemplate -endScrollLayout;
}

// Inspired by vrayAECompoundMultiNew in vrayAEFunctions.mel
// Makes control for Reset button and then calls updateTLAttributes
// and AELoadYarnTypesCompoundRepl (function that creates yrn_type controls).
global proc AELoadYarnTypesCompound(string $nodeDotAttr) {
	string $buffer[];
	tokenize($nodeDotAttr, ".", $buffer);
	string $nodeName = $buffer[0];

    columnLayout -adjustableColumn 1 tlMultiCompAttrFrame;
    //create frame for whole multi attribute
    frameLayout -l "Yarn Types" -collapse false vrayListFrame;
        //maya like -> row layout for "New Item" button
        rowLayout -nc 3 tlButtonLayout;
            separator -visible 0 "";
            button -label "Reset Yarn Types From File" -command ("setTLAttributesFromFile(\"" + $nodeDotAttr + "\")") tlResetYarnParamsBtn;
            button -label "Update File with Yarn Types" -command ("saveTLAttributesToFile(\"" + $nodeDotAttr + "\")") tlSaveYarnParamsBtn;
        setParent ..;
    setParent ..;
    setParent ..;

    AELoadYarnTypesCompoundRepl($nodeDotAttr);
}

// Helper function to add control for yarn_type parameter.
proc addParam(string $param, string $buffer, string $index, string $nodeName) {
    rowLayout -nc 2 -columnWidth2 50 350 -adjustableColumn 2;
        int $visible = 1;
        if ($index == "0") {
            $visible = 0;
        }
        string $ctrl_on = `checkBox -visible $visible -label "Used" ("yrn" + $buffer + $param + "On")`;
        connectControl $ctrl_on ($nodeName + "." + $param + "On" + $buffer);
        string $ctrl_f = `attrControlGrp -label $param -attribute ($nodeName + "." + $param + $buffer)`;
    setParent..;
}

// This is the Control Replace function. This is meant to be called when Maya
// wants to update the view, such as changing focus to a different instance of 
// the same node. 
// Here, we update reload button, delete any existing yarn_type controls and 
// create new ones. 
global proc AELoadYarnTypesCompoundRepl(string $nodeDotAttr) {

    // Update reset button with command to correct node.
	string $buffer[];
	tokenize($nodeDotAttr, ".", $buffer);
	string $nodeName = $buffer[0];
	button -e -command ("setTLAttributesFromFile(\"" + $nodeDotAttr + "\")") tlResetYarnParamsBtn;

    string $controlLayout = `setParent -q`;
    $controlLayout += ("|tlMultiCompAttrFrame|vrayListFrame");
	if (!`layout -exists $controlLayout`) {
			return;
	}

    // Delete existing yarn_type controls...
    string $currentChildren[] = `layout -q -childArray $controlLayout`;
    for($ch in $currentChildren) {
        if($ch != "tlButtonLayout") {
            deleteUI $ch;
        }
    }


    setParent $controlLayout;
    int $elementsCnt = `getAttr -size $nodeDotAttr`;
    if($elementsCnt > 0) {
		//	The return value of listAttr looks like this: {rgb[0], rgb[0].r, rgb[0].g, rgb[0].b, rgb[3], ... }
		// when we use it with -leaf results is something like { rgb[0], r, g, b, rgb[6] ...}
		string $multi[] = `listAttr -multi -leaf $nodeDotAttr`;
		for($m in $multi) {
            // Yarn type index as string with brackets [].
			string $buffer = match("\[[0-9]+\]", $m); 
            // Yarn type index
			int $index = match("[0-9]+", $m); 
			
            if(size($buffer) > 0) {
                string $frame_title = "Yarn Type " + $index;
                if($index == "0"){
                    $frame_title = "Default Yarn Type Settings";
                }
                string $yarn_frame = `frameLayout -collapse false -label $frame_title`;
	    		columnLayout;

                    // Specular color
                    rowLayout -nc 2 -columnWidth2 50 350 -adjustableColumn 2;
                        {
                            int $visible = 1;
                            if ($index == "0") {
                                $visible = 0;
                            }
                            string $ctrl_on = `checkBox -visible $visible -label "Used" ("yrn" + $buffer + "specularColorOn")`;
                            connectControl $ctrl_on ($nodeName + ".specularColorOn" + $buffer);
                        }
                        attrColorSliderGrp -label "specularColor" -attribute ($nodeName + ".specularColor" + $buffer);
                    setParent..;
                    addParam("specularColorAmount", $buffer, $index, $nodeName);

                    // Diffuse color
                    rowLayout -nc 2 -columnWidth2 50 350 -adjustableColumn 2;
                        {
                            int $visible = 1;
                            if ($index == "0") {
                                $visible = 0;
                            }
                            string $ctrl_on = `checkBox -visible $visible -label "Used" ("yrn" + $buffer + "diffuseColorOn")`;
                            connectControl $ctrl_on ($nodeName + ".diffuseColorOn" + $buffer);
                        }
                        attrColorSliderGrp -label "diffuseColor" -attribute ($nodeName + ".diffuseColor" + $buffer);
                    setParent..;
                    addParam("diffuseColorAmount", $buffer, $index, $nodeName);

                    // Add remaining float params.
                    string $params[] = {"bend", "yarnsize", "twist", "alpha", "specularNoise", "highlightWidth"};
                    for ($param in $params) {
                        addParam($param, $buffer, $index, $nodeName);
                    }
                setParent ..;
                setParent ..;
			}
		}
	}
	setParent ..;
	setParent ..;
}

global proc AESkipDefaultYrn(string $nodeDotAttr) {
	string $buffer[];
	tokenize($nodeDotAttr, ".", $buffer);
	string $nodeName = $buffer[0];

    rowLayout -nc 1 -columnWidth2 50 350 -adjustableColumn 1;
        checkBox -label "Keep default yarn parameters" tlSkipDefaultYrnChk;
    setParent..;
}
global proc AESkipDefaultYrnRepl(string $nodeDotAttr) {
	checkBox -e tlSkipDefaultYrnChk;
}

// File helper functions
global proc AEBrowseFileButton(string $nodeDotAttr) {
	string $buffer[];
	tokenize($nodeDotAttr, ".", $buffer);
	string $nodeName = $buffer[0];
    button -label "Browse File" -command ("browseFile(\"" + $nodeName + "\")") tlFileBrowseBtn;
}

global proc AEBrowseFileButtonRepl(string $nodeDotAttr) {
	string $buffer[];
	tokenize($nodeDotAttr, ".", $buffer);
	string $nodeName = $buffer[0];
	button -e -command ("browseFile(\"" + $nodeName + "\")") tlFileBrowseBtn;
}

global proc browseFile(string $nodeName) {
	string $filepath_before = `getAttr ($nodeName + ".filepath")`;
    string $loc = `fileDialog -dm "*"`;
    setAttr -type "string" ($nodeName + ".filepath") $loc;

    if ($filepath_before != $loc) {
        setTLAttributesFromFile($nodeName + ".filepath"); 
    }
}

